defaultTasks 'clean', 'test', 'sysTest'

group 'samples'
version '1'

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    sysTest {
        java {
            srcDir file('src/systest/java')
        }
        resources.srcDir file('src/systest/resources')
    }
}

task sysTest(type: Test, dependsOn: war) {
    testClassesDir = sourceSets.sysTest.output.classesDir
    classpath = sourceSets.sysTest.runtimeClasspath
    reports.html.destination = file("$buildDir/reports/systests")

    doFirst {
        jettyRunWar.contextPath = 'test';
        jettyRunWar.httpPort = 9080    // Port for test
        jettyRunWar.stopPort = 9082   // Port for stop signal
        jettyRunWar.daemon = true
        jettyRun.daemon = true
        jettyRunWar.execute()
    }

    doLast {
        jettyStop.stopPort = 9082   // Port for stop signal
        jettyStop.stopKey = 'stopKey'
        jettyStop.execute()
    }
}

dependencies {
    compile "com.fasterxml.jackson.core:jackson-core:2.7.1"
    compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-base:2.7.1"
    compile "org.glassfish.jersey.containers:jersey-container-servlet:2.22.2"
    compile "org.glassfish.jersey.media:jersey-media-json-jackson:2.22.2"

    testCompile group: 'junit', name: 'junit', version: '4.12'

    sysTestCompile sourceSets.main.output
    sysTestCompile configurations.testCompile
    sysTestCompile sourceSets.test.output
    sysTestRuntime configurations.testRuntime
}
